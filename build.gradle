plugins {
    id 'java-library'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.14'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'me.syfe'
version = '1.2.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    paperweight.foliaDevBundle('1.21.4-R0.1-SNAPSHOT')
}

def targetJavaVersion = 21

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    options.release.set(targetJavaVersion)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.register('deploy', Copy) {
    dependsOn build
    from 'build/libs'
    into './server/plugins'
    include '*.jar'
}